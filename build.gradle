plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id "maven-publish"
    id 'java'
}

group 'com.mmakowski'
version '1.0.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'com.google.guava', name: 'guava', version: '28.2-jre'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "mmakowski"
            name "Maciek Makowski"
            email "public@mmakowski.com"
        }
    }

    scm {
        url "https://github.com/mmakowski/proquint.git"
    }
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'proquint'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A Java Proquint library')
                root.appendNode('name', 'proquint')
                root.appendNode('url', 'https://github.com/mmakowski/proquint')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['BintrayPublication']
    pkg {
        repo = 'maven'
        name = 'proquint'
        version {
            name = project.version
        }
    }
}